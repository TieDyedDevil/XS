/* parse.y -- grammar for es ($Revision: 1.2 $) */

%{
#include "es.hxx"
#include "input.hxx"
#include "syntax.hxx"

typedef struct yyltype
{
  int first_line;
  int first_column;
  int last_line;
  int last_column;
} yyltype;
#define YYLTYPE yyltype
#define YYINITDEPTH 10000
%}

%error-verbose
%locations

%token	WORD QWORD
%token	LOCAL LET CLOSURE FN
%token	ANDAND BACKBACK EXTRACT CALL COUNT DUP FLAT OROR PRIM REDIR SUB ASSIGN
%token	NL ENDFILE ERROR 
%token  PARAM_BEGIN PARAM_END
%token  INT FLOAT ARITH_BEGIN ARITH_VAR

%left	LOCAL LET CLOSURE ')'
%left	ANDAND OROR NL
%left	'!'
%left	PIPE
%right	'$' 
%left	SUB

%left '+' '-'
%left '*' '/'

%union {
	Tree *tree;
	const char *str;
	NodeKind kind;
}

%type <str>	ARITH_VAR WORD QWORD INT FLOAT keyword
%type <tree>	REDIR PIPE DUP
		body cmd cmdsa cmdsan comword first line word param assign
		binding bindings params nlwords words simple redir sword
		arith
%type <kind>	binder

%start es

%%

es	: line end		{ parsetree = $1; YYACCEPT; }
	| error end		{ yyerrok; parsetree = NULL; YYABORT; }

end	: NL			{ if (!readheredocs(false)) YYABORT; }
	| ENDFILE		{ if (!readheredocs(true)) YYABORT; }

line	: cmd			{ $$ = $1; }
	| cmdsa line		{ $$ = mkseq("%seq", $1, $2); }

body	: cmd			{ $$ = $1; }
	| cmdsan body		{ $$ = mkseq("%seq", $1, $2); }

cmdsa	: cmd ';'		{ $$ = $1; }
	| cmd '&'		{ $$ = prefix("%background", mk(nList, thunkify($1), NULL)); }

cmdsan	: cmdsa			{ $$ = $1; }
	| cmd NL		{ $$ = $1; if (!readheredocs(false)) YYABORT; }

cmd	: %prec LET				{ $$ = NULL; }
	| simple				{ $$ = redirect($1); if ($$ == &errornode) YYABORT; }
	| redir cmd	%prec '!'		{ $$ = redirect(mk(nRedir, $1, $2)); 
						  if ($$ == &errornode) YYABORT; }
	| first assign				{ $$ = mk(nAssign, $1, $2); }
	| binder nl '(' bindings ')' nl cmd	{ $$ = mk($1, $4, $7); }
	| cmd ANDAND nl cmd			{ $$ = mkseq("%and", $1, $4); }
	| cmd OROR nl cmd			{ $$ = mkseq("%or", $1, $4); }
 	| cmd PIPE nl cmd			{ $$ = mkpipe($1, $2->u[0].i, $2->u[1].i, $4); }
	| '!' caret cmd				{ $$ = prefix("%not", mk(nList, thunkify($3), NULL)); }
	| '~' word words			{ $$ = mk(nMatch, $2, $3); }
	| EXTRACT word words			{ $$ = mk(nExtract, $2, $3); }

simple	: first				{ $$ = treecons2($1, NULL); }
	| simple  word	{ $$ = treeconsend2($1, $2); }
	| simple  redir	{ $$ = redirappend($1, $2); }

redir	: DUP				{ $$ = $1; }
	| REDIR word			{ $$ = mkredir($1, $2); }

bindings: 				{ $$ = NULL; }
	| binding			{ $$ = treecons2($1, NULL); }
	| bindings ';' binding		{ $$ = treeconsend2($1, $3); }
	| bindings NL binding		{ $$ = treeconsend2($1, $3); }


binding	: word assign			{ $$ = mk(nAssign, $1, $2); }
	| word				{ $$ = mk(nAssign, $1, NULL); }

assign	: caret ASSIGN caret words	{ $$ = $4; }


first	: comword			{ $$ = $1; }
	| first '^' sword		{ $$ = mk(nConcat, $1, $3); }

sword	: comword			{ $$ = $1; }
	| keyword			{ $$ = mk(nWord, $1); }

word	: sword				{ $$ = $1; }
	| word '^' sword		{ $$ = mk(nConcat, $1, $3); }

comword	: param				{ $$ = $1; }
	| '(' nlwords ')'		{ $$ = $2; }
	| '{' body '}'			{ $$ = thunkify($2); }
	| '{' caret PARAM_BEGIN params caret PARAM_END caret body '}'	
					{ $$ = mklambda($4, $8); }
	| '$' sword			{ $$ = mk(nVar, $2); }
	| '$' sword SUB words ')'	{ $$ = mk(nVarsub, $2, $4); }
	| ARITH_BEGIN arith ')'		{ $$ = mk(nArith, $2); }
	| CALL sword			{ $$ = mk(nCall, $2); }
	| COUNT sword			{ $$ = mk(nCall, prefix("%count", treecons(mk(nVar, $2), NULL))); }
	| FLAT sword			{ $$ = flatten(mk(nVar, $2), " "); }
	| PRIM WORD			{ $$ = mk(nPrim, $2); }
	| '`' sword			{ $$ = backquote(mk(nVar, mk(nWord, "ifs")), $2); }
	| BACKBACK word	sword		{ $$ = backquote($2, $3); }

arith   : arith '+' arith		{ $$ = mk(nPlus, $1, $3); }
	| arith '-' arith		{ $$ = mk(nMinus, $1, $3); }
	| arith '*' arith		{ $$ = mk(nMult, $1, $3); }
	| arith '/' arith		{ $$ = mk(nDivide, $1, $3); }
	| '(' arith ')'			{ $$ = $2; }
	| ARITH_VAR			{ $$ = mk(nVar, mk(nWord, $1)); }
	| INT				{ $$ = mk(nInt, $1); }
	| FLOAT				{ $$ = mk(nFloat, $1); }

param	: WORD				{ $$ = mk(nWord, $1); }
	| QWORD				{ $$ = mk(nQword, $1); }

params	:				{ $$ = NULL; }
	| params param			{ $$ = treeconsend($1, $2); }

words	:				{ $$ = NULL; }
	| words word			{ $$ = treeconsend($1, $2); }

nlwords :				{ $$ = NULL; }
	| nlwords word			{ $$ = treeconsend($1, $2); }
	| nlwords NL			{ $$ = $1; }

nl	:
	| nl NL

caret 	:
	| '^'

binder	: LOCAL	        { $$ = nLocal; }
	| LET		{ $$ = nLet; }
	| CLOSURE	{ $$ = nClosure; }

keyword	: '!'		{ $$ = "!"; }
	| '~'		{ $$ = "~"; }
	| EXTRACT	{ $$ = "~~"; }
        | LOCAL 	{ $$ = "local"; }
	| LET		{ $$ = "let"; }
	| CLOSURE	{ $$ = "%closure"; }

